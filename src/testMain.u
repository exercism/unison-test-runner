use testRunner TestFile Test

{{
  {testRunner.main} is the entry point function that the transcript should call to write the json output file.

  It relies on the existence of environment variables, ''results_file'' and ''solution_dir'' set in the test runner script.

  the value ''tests'' is dependent upon a loaded value from the solutions folder.
}}
testRunner.main : '{IO,Exception}()
testRunner.main _ =
  use FilePath /
  solutionPrefix = IO.getEnv "solution_dir" |> FilePath
  testAnnotationFilePath = solutionPrefix / ".meta" / "testAnnotation.json"
  annotations = !(parseAnnotationFile testAnnotationFilePath)
  testRunner.checkAnnotationFile annotations tests
  testFileJson = toV2TestFile tests annotations |> TestFile.toJson
  filePath = IO.getEnv "results_file" |> FilePath
  writeFileUtf8 filePath (Json.toText testFileJson)

testRunner.Result.toTest: Result -> TestAnnotation -> Test
testRunner.Result.toTest result annotation =
  use testRunner.Test Test
  (TestAnnotation name code taskId) = annotation
  match result with
    test.Result.Fail msg ->
      failureMsg = if msg === "" then "Expected value was not equal to result" else msg
      Test name (Some code) testRunner.Status.Fail (Some (Message failureMsg)) None taskId
    test.Result.Ok msg ->
      Test name (Some code) testRunner.Status.Pass None None taskId

testRunner.toV2TestFile : [Result] -> [TestAnnotation] -> TestFile
testRunner.toV2TestFile results names =
  use testRunner.TestFile
  tests = List.zipWith Result.toTest results names
  containsFail = List.find (cases (testRunner.Test.Test a b s d e f) ->
    s === Status.Fail ) tests |> isSome
  status = if containsFail then Status.Fail else Status.Pass
  TestFile Version.Two status None (Some tests)

testRunner.checkAnnotationFile : [a] -> [Result] -> {Exception}()
testRunner.checkAnnotationFile annotationlist tests =
  use Text ++
  annotationSize = List.size annotationlist
  testsSize = List.size tests
  failureMsg = "test file contained " ++ (Nat.toText testsSize) ++ " items but annotations file contained " ++ (Nat.toText annotationSize) ++ " items"
  if (annotationSize Nat.!= testsSize) then raise (failure failureMsg (annotationlist, tests)) else ()

{{Assumes a json file containing {type TestAnnotation} in a list. The order must match the order of the tests value }}
testRunner.parseAnnotationFile : FilePath -> '{IO, Exception} [TestAnnotation]
testRunner.parseAnnotationFile filePath _ =
  rawJson = readFileUtf8 filePath
  (TestAnnotationFile annotations) = Decoder.run testAnnotationFileDecoder rawJson
  annotations
